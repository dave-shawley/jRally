<?xml version="1.0" encoding="UTF-8"?>
<!-- Mark A. Ziesemer, http://www.ziesemer.com + 2008-03-25 -->
<xsl:transform version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:xs="http://www.w3.org/2001/XMLSchema" xmlns:tlib="http://java.sun.com/xml/ns/javaee" exclude-result-prefixes="tlib">
	<xsl:output method="xml" version="1.0" encoding="UTF-8" indent="yes"/>
	<xsl:template match="/tlib:taglib">
		<xsl:comment> Generated by TaglibToXSD. + Mark A. Ziesemer, http://www.ziesemer.com + 2008-03-25 </xsl:comment>
	
		<xs:schema version="1.0" xml:lang="en" elementFormDefault="qualified">
			<xsl:attribute name="targetNamespace"><xsl:value-of select="normalize-space(tlib:uri)"/></xsl:attribute>
			<xs:annotation>
				<xs:documentation><xsl:value-of select="normalize-space(tlib:description)"/></xs:documentation>
				<xs:documentation>tlib-version: <xsl:value-of select="normalize-space(tlib:tlib-version)"/></xs:documentation>
			</xs:annotation>
			<xsl:apply-templates select="tlib:tag"/>
		</xs:schema>
	</xsl:template>
	
	<xsl:template match="tlib:tag">
		<xs:element>
			<xsl:attribute name="name"><xsl:value-of select="normalize-space(tlib:name)"/></xsl:attribute>
			<xs:annotation>
				<xs:documentation>
					<xsl:text disable-output-escaping="yes">&lt;![CDATA[</xsl:text>
					<xsl:value-of select="tlib:description" disable-output-escaping="yes"/>
					<xsl:text disable-output-escaping="yes">]]&gt;</xsl:text>
				</xs:documentation>
				<xs:documentation>tag-class: <xsl:value-of select="normalize-space(tlib:tag-class)"/></xs:documentation>
				<xs:documentation>body-content: <xsl:value-of select="normalize-space(tlib:body-content)"/></xs:documentation>
			</xs:annotation>
			<xs:complexType>
				<xsl:if test="not(tlib:body-content='empty')">
					<xsl:attribute name="mixed">true</xsl:attribute>
					<xs:choice minOccurs="0" maxOccurs="unbounded">
						<xs:any processContents="lax"/>
					</xs:choice>
				</xsl:if>
				<xsl:apply-templates select="tlib:attribute"/>
			</xs:complexType>
		</xs:element>
	</xsl:template>
	
	<xsl:template match="tlib:attribute">
		<xs:attribute>
			<xsl:attribute name="name"><xsl:value-of select="normalize-space(tlib:name)"/></xsl:attribute>
			<xsl:if test="normalize-space(tlib:required)='true'">
				<xsl:attribute name="use">required</xsl:attribute>
			</xsl:if>
			<xs:annotation>
				<xs:documentation>
					<xsl:text disable-output-escaping="yes">&lt;![CDATA[</xsl:text>
					<xsl:value-of select="tlib:description" disable-output-escaping="yes"/>
					<xsl:text disable-output-escaping="yes">]]&gt;</xsl:text>
				</xs:documentation>
			</xs:annotation>
		</xs:attribute>
	</xsl:template>
</xsl:transform>
